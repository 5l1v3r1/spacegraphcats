#
# Snakemake configuration file for running spacegraphcats pipelines.
#
# see script 'run' in this directory for a convenient entry point.
#
# Quickstart: `snakeconf/run snakeconf/dory-test.json searchquick`
#

## pull values in from config file:

# name of catlas
catlas_base = config['catlas_base']

# sequence files to use when building catlas
input_sequences = config['input_sequences']

# k-mer size to use for everything
ksize = config['ksize']

# radius for catlas building
radius = config['radius']

# seeds to use when building minhash databases
searchseeds = config.get('searchseeds', 43)

# search overhead
overhead = config.get('overhead', 0.0)

# suffix to add to search output
experiment = config.get('experiment', '')
search_out_suffix = ''
if experiment:
    search_out_suffix = '_' + experiment

### build some variables for internal use

catlas_dir = '{}_k{}_r{}'.format(catlas_base, ksize, radius)
search_dir = '{}_k{}_r{}_search_oh{}{}'.format(catlas_base, ksize, radius, int(overhead*100), search_out_suffix)

# internal definitions for convenience:
python=sys.executable  # define as the version of Python running snakemake

###############################################################################
## some utility functions.

# rewrite the datasets into bcalm format
def write_bcalm_in(datasets):
    x = []
    for d in datasets:
        x.append('-in {}'.format(d))
    return " ".join(x)


def parse_seeds(seeds_str):
    seeds = []
    seeds_str = seeds_str.split(',')
    for seed in seeds_str:
        if '-' in seed:
            (start, end) = seed.split('-')
            for s in range(int(start), int(end) + 1):
                seeds.append(s)
        else:
            seeds.append(int(seed))

    return seeds


###############################################################################

## now, build some variables...

BCALM_INPUT=write_bcalm_in(input_sequences)
SEEDS=parse_seeds(config['searchseeds'])

### rules!

# build catlas & minhashes needed for search
rule build:
    input:
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/minhashes.db.k{ksize}.s1000.abund0.seed{seed}", catlas_dir=catlas_dir, seed=SEEDS, ksize=ksize),
        expand("{catlas_dir}/minhashes.db.k{ksize}.var.seed0", catlas_dir=catlas_dir, ksize=ksize)

# build cDBG using bcalm
rule bcalm_cdbg:
     input:
        input_sequences
     output:
        "bcalm.{catlas_dir}.k{ksize}.unitigs.fa"
     shell:
        # @CTB here we run into the problem that bcalm wants
        # '-in file1 -in file2', so I am using 'params' and 
        "bcalm {BCALM_INPUT} -out bcalm.{wildcards.catlas_dir}.k{wildcards.ksize} -kmer-size {wildcards.ksize} -abundance-min 1 >& {output}.log.txt"

# build catlas input from bcalm output by reformatting
rule bcalm_catlas_input:
     input:
        expand("bcalm.{{catlas_dir}}.k{ksize}.unitigs.fa", ksize=ksize)
     output:
        "{catlas_dir}/cdbg.gxt",
        "{catlas_dir}/contigs.fa.gz"
     shell:
        "{python} -m search.bcalm_to_gxt {input} {catlas_dir}/cdbg.gxt {catlas_dir}/contigs.fa.gz"


# build catlas!
rule build_catlas:
     input:
        "{catlas_dir}/cdbg.gxt",
        "{catlas_dir}/contigs.fa.gz",
     output:
        "{catlas_dir}/first_doms.txt",
        "{catlas_dir}/catlas.csv"
     shell:
        "{python} -m spacegraphcats.catlas {catlas_dir} {radius}"

# build minhash databases
rule minhash_db:
     input:
        "{catlas_dir}/cdbg.gxt",
        "{catlas_dir}/contigs.fa.gz",
        "{catlas_dir}/first_doms.txt",
     output:
        "{catlas_dir}/minhashes.db.k{ksize}.s1000.abund0.seed{seed}"
     shell:
        "{python} -m search.make_catlas_minhashes -k {wildcards.ksize} --seed={wildcards.seed} --scaled=1000 {catlas_dir}"

# build minhash databases
rule minhash_db_var:
     input:
        "{catlas_dir}/cdbg.gxt",
        "{catlas_dir}/contigs.fa.gz",
        "{catlas_dir}/first_doms.txt",
     output:
        "{catlas_dir}/minhashes.db.k{ksize}.var.seed0"
     shell:
        "{python} -m search.make_catlas_minhashes_var -k {wildcards.ksize} --num=10 {catlas_dir}"

### Search rules.

def make_query_base(catlas_dir, searchfiles):
    x = []
    for filename in searchfiles:
        x.append("{}/{}.contigs.sig".format(search_dir, os.path.basename(filename)))
    return x

# do a quick search!
rule searchquick:
    input:
        config['searchquick'],
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/minhashes.db.k{ksize}.var.seed0", catlas_dir=catlas_dir, ksize=ksize),
        expand("{catlas_dir}/minhashes.db.k{ksize}.s1000.abund0.seed{seed}", catlas_dir=catlas_dir, seed=SEEDS, ksize=ksize),
    output:
        expand("{search_dir}/results.csv", search_dir=search_dir),
        make_query_base(catlas_dir, config['searchquick']),
    shell:
        "{python} -m search.extract_nodes_by_query {catlas_dir} {search_dir} --query {config[searchquick]} --seed={searchseeds} -k {ksize} --overhead={overhead}"


# do a full search!
rule search:
    input:
        config['search'],
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/minhashes.db.k{ksize}.s1000.abund0.seed{seed}", catlas_dir=catlas_dir, seed=SEEDS, ksize=ksize),
        expand("{catlas_dir}/minhashes.db.k{ksize}.var.seed0", catlas_dir=catlas_dir, ksize=ksize)
    output:
        expand("{search_dir}/results.csv", search_dir=search_dir),
        make_query_base(catlas_dir, config['search']),
    shell:
        "{python} -m search.extract_nodes_by_query {catlas_dir} {search_dir} --query {config[search]} --seed={searchseeds} -k {ksize} --overhead={overhead}"


### shadow ratio stuff

rule shadow_ratio:
    input:
        expand("{catlas_dir}.shadow.{maxsize}.fa",
               catlas_dir=catlas_dir,
               maxsize=config.get('shadow_ratio_maxsize', 1000))

rule extract_by_shadow_ratio_rule:
    input:
        expand("{catlas_dir}/first_doms.txt", catlas_dir=catlas_dir),
        expand("{catlas_dir}/catlas.csv", catlas_dir=catlas_dir),
        expand("{catlas_dir}/minhashes.db.k{ksize}.s1000.abund0.seed43", catlas_dir=catlas_dir, ksize=ksize),
    output:
        "{catlas_dir}.shadow.{shadow_ratio_maxsize}.fa"
    shell:
        "{python} -m search.extract_nodes_by_shadow_ratio --maxsize={wildcards.shadow_ratio_maxsize} {catlas_dir} {output}"
